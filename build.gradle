apply plugin: 'java'

task fatJar(type: Jar) {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Main-Class': 'org.mumue.mumue.Main'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'org.mumue.mumue.Main'
    }
}

repositories {
    mavenCentral()
}

def excludeHamcrest = { exclude module: 'hamcrest-core' }

dependencies {
    // General
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.inject:guice:4.0'

    // Database
    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile 'commons-dbutils:commons-dbutils:1.6'
    compile 'com.h2database:h2:1.4.190'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12', excludeHamcrest
    testCompile 'org.mockito:mockito-core:1.10.19', excludeHamcrest
    testCompile 'org.unitils:unitils-core:3.4.2', excludeHamcrest
    testCompile 'commons-net:commons-net:3.4'
}
